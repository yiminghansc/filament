cmake_minimum_required(VERSION 3.19)
project(filamesh)

set(TARGET filamesh)

# ==================================================================================================
# Sources
# ==================================================================================================
set(SRCS
    src/MeshWriter.cpp
    src/MeshWriter.h
    src/main.cpp)

# ==================================================================================================
# Target definitions
# ==================================================================================================
add_executable(${TARGET} ${SRCS})

set_target_properties(${TARGET} PROPERTIES DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX})

target_link_libraries(${TARGET} PUBLIC math)
target_link_libraries(${TARGET} PUBLIC utils)
if(HUNTER_ENABLED)
  target_link_libraries(${TARGET} PUBLIC Assimp::assimp)
  target_link_libraries(${TARGET} PRIVATE filameshio meshoptimizer::meshoptimizer)
  if (MSVC)
    target_link_libraries(${TARGET} PRIVATE getopt::getopt)
  endif()
else()
  target_link_libraries(${TARGET} PUBLIC assimp)
  target_link_libraries(${TARGET} PRIVATE getopt filameshio meshoptimizer)
endif()

# ==================================================================================================
# Compile options and optimizations
# ==================================================================================================
if(NOT MSVC)
    target_compile_options(${TARGET} PRIVATE -Wno-deprecated-register)

    if (NOT LINUX)
        target_compile_options(${TARGET} PRIVATE
                -Wno-address-of-packed-member
        )
    endif()
endif()

# =================================================================================================
# Licenses
# ==================================================================================================
set(MODULE_LICENSES getopt libassimp meshoptimizer)
set(GENERATION_ROOT ${CMAKE_CURRENT_BINARY_DIR}/generated)
list_licenses(${GENERATION_ROOT}/licenses/licenses.inc ${MODULE_LICENSES})
target_include_directories(${TARGET} PRIVATE ${GENERATION_ROOT})

# ==================================================================================================
# Installation
# ==================================================================================================
install(TARGETS ${TARGET} EXPORT ${TARGETS_EXPORT_NAME} RUNTIME DESTINATION bin)
install(FILES "README.md" DESTINATION docs/ RENAME "${TARGET}.md")
